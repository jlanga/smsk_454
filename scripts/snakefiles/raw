rule raw_make_link_sff_sample:
    """
    Create links from original file to other one in data/sff/{sample_name}.sff
    """
    input:
        sff = lambda wildcards: config["samples"][wildcards.sample]["sff"]
    output:
        sff = protected(raw_dir + "{sample}.sff")
    log:
        raw_doc + "make_link_sff_{sample}.log"
    benchmark:
        raw_doc + "make_link_sff_{sample}.json"
    shell:
        "ln --symbolic "
            "$(readlink --canonicalize {input.sff} "
            "{output.sff}) "
        "2> {log}"



rule raw_sff_to_fastq_sample:
    """
    Convert original sff to fastq.gz
    """
    input:
        sff= raw_dir + "{sample}.sff"
    output:
        fq_gz= protected(raw_dir + "{sample}.raw.fq.gz")
    threads:
        1
    log:
        raw_doc + "sff_to_fastq_{sample}.log"
    benchmark:
        raw_doc + "sff_to_fastq_{sample}.json"
    shell:
        "(python3 scripts/fasta_qual_to_fastq.py "
            "<(./src/454/bin/sffinfo -s {input.sff}) "
            "<(./src/454/bin/sffinfo -q {input.sff}) | "
        "pigz "
            "--best "
        "> {output.fq_gz} ) "
        "2> {log}"




rule raw_make_link_adaptors:
    """
    Create link to from the original adaptor file to data/adaptors/adaptors.fa
    """
    input:
        fa = config["adaptors"]
    output:
        fa = protected(raw_dir + "adaptors.fa")
    threads:
        1
    log:
        raw_doc + "make_link_adaptors.log"
    benchmark:
        raw_doc + "make_link_adaptors.json"
    shell:
        "ln --symbolic "
            "$(readlink --canonicalize {input.fa}) "
            "{output.fa} "
        "2> {log}"
